
/*
 * This file is part of i42
 * Copyright (c) 2022 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import jk.base64

class is AzureClient:

model VirtualMachineProperties public
{
	name as string
	hostname as string
	nicId as string
	configureScript as string
	adminUsername as string
	adminPassword as string
	sshPublicKey as string
	vmSize as string
}

func updateVirtualMachine(properties as VirtualMachineProperties) async as dynamic
{
	var client = AzureApi.getComputeManagementClient(context)
	var osProfile = {
		adminUsername String.isEmpty(properties.adminUsername) ? "vmuser" : properties.adminUsername
		adminPassword properties.adminPassword
		computerName String.isEmpty(properties.hostname) ? properties.name : properties.hostname
		linuxConfiguration {
			disablePasswordAuthentication String.isEmpty(properties.adminPassword) ? true : false
		}
	}
	if String.isNotEmpty(properties.sshPublicKey) {
		osProfile.ssh = {
			publicKeys [
				{
					keyData properties.sshPublicKey
				}
			]
		}
	}
	if String.isNotEmpty(properties.configureScript):
		osProfile.customData = Base64Encoder.encodeString(properties.configureScript)
	var params = {
		location context.location
		osProfile osProfile
		hardwareProfile {
			vmSize properties.vmSize
		}
		storageProfile {
			imageReference {
				offer "0001-com-ubuntu-server-focal"
				sku "20_04-lts"
				publisher "Canonical"
				version "latest"
			}
			osDisk {
				caching "ReadWrite"
				managedDisk {
					storageAccountType "Standard_LRS"
				}
				createOption "FromImage"
				name properties.name .. "-osdisk"
			}
		}
		diagnosticsProfile {
			bootDiagnostics {
				enabled true
			}
		}
	}
	if String.isNotEmpty(properties.nicId) {
		params.networkProfile = {
			networkInterfaces [
				{
					id properties.nicId
				}
			]
		}
	}
	return await executeUpdate("Virtual Machine", func async as dynamic {
		await client.virtualMachines.beginCreateOrUpdateAndWait(context.resourceGroup, properties.name, params)
		return await client.virtualMachines.get(context.resourceGroup, properties.name)
	})
}
